//Keegan
//Blendshape Editor
//blendshapeEditor
//none
//blendshapeEditor
//animation
/*
Author: David Keegan (keegan3d@gmail.com)
Copyright (C) 2005 David Keegan

Version: 1.0

Function: Allows the user to edit multiple blendshapes
	simultaneously.

Last Updated: (07-07-05)

Install
	1. Move blendshapeEditor.mel to you Maya scripts directory.
	2. Type "blendshapeEditor" into Maya's command line.
*/

global proc blendshapeEditor()
{
string $sel[] = `ls -sl`;
string $shape1[] = `listRelatives -s $sel[0]`;

int $a=0;
int $sizeSel = `size($sel)`;

progressWindow
	-t "Connecting Shape..."
	-pr $a
	-st "Connecting: ..."
	-ii 1
	;

for($i=1; $i<$sizeSel; $i++)
	{
		if(`progressWindow -q -ic`)
			break;
	
		$a++;
	
		progressWindow -edit -max $sizeSel -pr $a -st ("Connecting: " + $sel[$i]);		

		string $dup[] = `duplicate -n ($sel[$i] + "_Replace") $sel[$i]`;
		
		string $shape2[] = `listRelatives -s $sel[$i]`;
		connectAttr -f ($shape1[0] + ".outMesh") ($shape2[0] + ".inMesh");
		
		int $numVerts[] = `polyEvaluate -v $sel[$i]`;
		
		for($v=0; $v<$numVerts[0]; $v++)
			{
				vector $dupVertPos = `pointPosition -w ($dup[0] + ".vtx[" + $v + "]")`;

				move -ws ($dupVertPos.x) ($dupVertPos.y) ($dupVertPos.z) ($sel[$i] + ".vtx[" + $v + "]");
			}
		delete $dup;
	}
progressWindow -ep;
}
